plugins {
    id "org.jetbrains.kotlin.jvm" version "1.6.10"
    id "idea"
    id "application"
}

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    typedConfigGenerator
}

application {
    mainClass = "ApplicationKt"
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "com.github.nanodeath.typedconfig:runtime:1.0-SNAPSHOT"
    typedConfigGenerator "com.github.nanodeath.typedconfig:generator:1.0-SNAPSHOT"
}

task generateConfig(type: JavaExec) {
    inputs.file("config.tc.toml")
    outputs.dir("$buildDir/generated-sources")
    classpath = configurations.typedConfigGenerator
    mainClass.set("com.github.nanodeath.typedconfig.generate.Generator")
    args("config.tc.toml", "$buildDir/generated-sources")
}

tasks.withType(AbstractCompile) {
    dependsOn("generateConfig")
}

sourceSets {
    main {
        java {
            srcDir("$buildDir/generated-sources")
        }
    }
}

idea {
    module {
        generatedSourceDirs += file("$buildDir/generated-sources")
    }
}
